px = 5

Print["Generating all tress up to order ", px]
SetAttributes[L, Orderless];
o[L[t___], s_] := L[t, s]; (* Butcher product of two trees *)
tau = L[];
t[1] = tau;
ts[1] = {tau}; (* trees grouped by order *)
allts = {tau}; (* all trees *)
check[{p_, t_}] := If[Not[MemberQ[allts, t]], AppendTo[allts, t]; AppendTo[ts[p], t]]

For[p = 2, p <= px, p++,
    ts[p] = {};
    For[i = 1, i < p, i++, 
       check /@ Flatten[Table[{p, o[s, t]}, {s, ts[p - i]}, {t, ts[i]}], 1]
    ]
]
Print["How many are there?", Length /@ ts /@ Range[px]]

order[tau] = 1 
order[t_] := 1 + Plus@@(order/@(List@@t))
factorial[tau] = 1
factorial[t_] := order[t] * Times@@(factorial/@(List@@t))

phi[tau] := Plus@@b
phi[t_]  := b . Times@@phiA/@(List@@t)
phiA[tau]:= c
phiA[t_] := A . Times@@phiA/@(List@@t)

Print["Checking the ambiguous method of page 185"]

A = {
{0,0,0,0,0,0},
{1/2, 0, 0, 0, 0, 0},
{-9/4, 13/4, 0, 0, 0, 0},
{9/64, 5/32, -3/64, 0, 0, 0},
{63/625, 259/2500, 231/2500, 252/625, 0, 0},
{-27/50, -139/50, -21/50, 56/25, 5/2, 0}}

c = Plus @@@ A;
b = {1/14, 0, 0, 32/81, 250/567, 5/54};

Print["Order residuals"]
Print[phi/@allts - 1/(factorial/@allts)]

(* Results:

â¯ /usr/local/bin/math
Mathematica 12.0.0 Kernel for Linux x86 (64-bit)
Copyright 1988-2019 Wolfram Research, Inc.

In[1]:= <<Check.math
Generating all tress up to order 5
How many are there?{1, 1, 2, 4, 9}
Checking the ambiguous method of page 185
Order residuals
                                   3            3
{0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, ---, 0, 0, -(---), 0, 0}
                                  320          320
*)
